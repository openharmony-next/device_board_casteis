# Copyright (c) 2021-2023 HiHope Open Source Organization .
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/clang/clang.gni")
import("//build/ohos.gni")
kernel_build_script_dir = "//kernel/linux/${linux_kernel_version}"
kernel_source_dir = "//kernel/linux/${linux_kernel_version}"

action("kernel") {
  script = "build_kernel.sh"
  sources = [ kernel_source_dir ]

  product_path = "vendor/$product_company/$product_name"
  outputs = [
    "$root_build_dir/../kernel/src_tmp/${linux_kernel_version}/boot_linux",
    "$root_build_dir/../kernel/checkpoint/compile_check",
  ]
  args = [
    rebase_path(kernel_build_script_dir, root_build_dir),
    rebase_path("$root_build_dir/packages/phone/images"),
    rebase_path("//device/board/hihope/$device_name"),
    product_path,
    rebase_path("$root_build_dir/../.."),
    device_company,
    device_name,
    product_company,
    "${linux_kernel_version}",
    build_variant,
    device_type,
  ]
  if (enable_lto_O0) {
    args += [ "enable_lto_O0" ]
  } else {
    args += [ "disable_lto_O0" ]
  }
  if (enable_ramdisk) {
    args += [ "enable_ramdisk" ]
  }

  if (enable_mesa3d) {
    args += [ "enable_mesa3d" ]
  }
  if (enable_absystem) {
    args += [ "enable_absystem" ]
  }
  if (build_variant == "user") {
    args += [ "is_release" ]
  }
}
